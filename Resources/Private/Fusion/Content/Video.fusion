prototype(Jonnitto.PrettyEmbedVideoPlatforms:Content.Video) < prototype(Jonnitto.PrettyEmbedHelper:Content) {
    package = 'VideoPlatforms'

    platform = ${q(node).property('platform')}
    type = ${q(node).property('type')}
    videoID = ${q(node).property('videoID')}

    metadataID = ${q(node).property('metadataID')}
    metadataTitle = ${q(node).property('metadataTitle')}
    alternativeText = ${this.metadataTitle}
    imageFromPlatform = ${q(node).property('metadataImage')}
    thumbnailFromResources = ${q(node).property('metadataThumbnail')}
    ratio = ${q(node).property('metadataRatio')}
    async = true

    // This can used as alternative preview
    content = null

    loadImageStrategy = ${Configuration.setting('Jonnitto.PrettyEmbedVideoPlatforms.loadImageStrategy')}
    enableGdprMessage = ${Configuration.setting('Jonnitto.PrettyEmbedHelper.enableGdprMessage')}
    gdprMessage = Jonnitto.PrettyEmbedHelper:GdprMessage

    showBackendNotification = true

    // Should the video be opened on a lightbox?
    lightbox = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'lightbox'
    }

    // When the lightbox is set, should the preview image preserve his aspect ratio?
    preserveAspectRatio = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'preserveAspectRatio'
    }

    // If no aspect ratio can be calcualted from the oembed service, you have the possibility to force the aspect ratio 16:9
    force16to9 = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'force16to9'
    }

    // Whether to restart the video automatically after reaching the end.
    loop = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'loop'
    }

    // Whether the video plays inline on supported mobile devices.
    // Note that if set to false it does not imply that the video will always be played in fullscreen.
    playsinline = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'playsinline'
    }

    // Allow full screen
    allowFullScreen = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'allowFullScreen'
    }

    //////////////////////////////////
    // Additional settings for Youtube
    //////////////////////////////////

    // This parameter indicates whether the video player controls are displayed
    controls = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'controls'
    }

    // Setting the parameter's value to true causes closed captions to be shown by default,
    // even if the user has turned captions off.
    closedCaptions = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'closedCaptions'
    }

    // It is not possible to disable related videos, but if it is set to false, the player will
    // show related videos that are from the same channel as the video that was just played.
    showRelated = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'showRelated'
    }

    ////////////////////////////////
    // Additional settings for Vimeo
    ////////////////////////////////

    // Whether to pause the current video when another Vimeo video on the same page starts to play.
    // Set this value to false to permit simultaneous playback of all the videos on the page.
    autopause = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'autopause'
    }

    // Whether the player is in background mode, which hides the playback controls, enables autoplay, and loops the video.
    background = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'background'
    }

    // Whether the video is muted upon loading. The true value is required for the autoplay behavior in some browsers.
    muted = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'muted'
    }

    // Whether the responsive player and transparent background are enabled
    transparent = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'transparent'
    }

    // The hexadecimal color value of the playback controls, which is normally 00ADEF.
    color = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'color'
    }

    // Whether the player displays the title overlay
    previewTitle = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'previewTitle'
    }

    // Whether to display the video owner's name.
    previewByline = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'previewByline'
    }

    // Whether to display the video owner's portrait.
    previewPortrait = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'previewPortrait'
    }

    /*
        These properties are already defined in Jonnitto.PrettyEmbedHelper:Content
        - maximumWidth
        - wrapper
        - origin
        - image
    */

    // Internal
    videoID.@process.getIDFromUrl = ${this.metadataID ? this.metadataID : (value && this.platform ? Jonnitto.PrettyEmbedHelper.platformID(value, this.platform) : value)}

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        poster = Neos.Fusion:Case {
            hasAlternativePreview {
                condition = ${props.content}
                renderer = true
            }
            hasPersistentResource {
                condition = ${props.image || props.thumbnailFromResources}
                renderer = Neos.Neos:ImageUri {
                    asset = ${props.image || props.thumbnailFromResources}
                    async = ${props.async}
                    maximumWidth = ${props.maximumWidth}
                }
            }
            imageFromVimeo {
                condition = ${props.videoID && props.platform == 'vimeo'}
                renderer = ${props.imageFromPlatform || Jonnitto.PrettyEmbedHelper.vimeoThumbnail(props.videoID)}
            }
            imageFromPlatform {
                condition = ${props.videoID}
                renderer = ${props.imageFromPlatform}
            }
        }
        poster2x = Neos.Neos:ImageUri {
            @if {
                hasAsset = ${this.asset}
                hasEnoughPixel = ${this.asset.width >= props.maximumWidth * 2}
            }
            asset = ${props.image || props.thumbnailFromResources}
            async = ${props.async}
            maximumWidth = ${props.maximumWidth * 2}
        }
        renderer = Neos.Fusion:Case {
            noVideoID {
                condition = ${!props.videoID || !props.platform}
                renderer = Jonnitto.PrettyEmbedHelper:Notification {
                    @if.set = ${props.showBackendNotification}
                    key = 'noVideoID'
                    default = 'Please add a Video URL / ID or url in the inspector'
                }
            }
            noPoster {
                condition = ${!props.poster && (props.platform == 'youtube' ? props.type == 'playlist' : true)}
                renderer = Jonnitto.PrettyEmbedHelper:Notification {
                    key = 'needPreviewImage'
                    default = 'You need to define a custom preview image.'
                }
            }
            default {
                condition = true
                renderer = afx`<Jonnitto.PrettyEmbedVideoPlatforms:Component.Video {...props} live={node.context.live} />`
            }
        }
    }
}
